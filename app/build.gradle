plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
}

// Creates a variable called keystorePropertiesFile, and initializes it to the
// keystore.properties file.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initializes a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Loads the keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 33

    ext {
        versionMajor = 1
        versionMinor = 0
        versionPatch = 3
    }

    defaultConfig {
        applicationId "com.tinytotsnbites.papapoints"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 5
        versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
        setProperty("archivesBaseName","PapaPoints-${versionName}")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    /*  Uncomment the below block to rename all the variants of the apk
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "PapaPoints.apk"
        }
    } */

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            debuggable true
            resValue 'string', 'app_name', '"Papa Points Debug"'
        }
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue 'string', 'app_name', '"Papa Points"'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    def room_version = "2.5.0"
    def work_version = "2.8.0"
    def splashScreenVersion = "1.0.0"

    implementation 'io.github.ParkSangGwon:tedimagepicker:1.4.2'
    implementation "com.github.bumptech.glide:glide:4.12.0"
    implementation 'com.google.firebase:firebase-common-ktx:20.3.0'
    implementation 'com.google.firebase:firebase-analytics-ktx:21.2.0'
    implementation "androidx.core:core-splashscreen:$splashScreenVersion"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    // To use Kotlin annotation processing tool (kapt)
    kapt "androidx.room:room-compiler:$room_version"
    // To use Kotlin Symbol Processing (KSP)
   // ksp "androidx.room:room-compiler:$room_version"

    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}